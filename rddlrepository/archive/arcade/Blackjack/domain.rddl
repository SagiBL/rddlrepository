////////////////////////////////////////////////////////////////////
// In the classic game of blackjack (or 21), cards are randomly drawn
// from a deck (with replacement) and added to the player's hand.
// The player decides whether to continue drawing or stop. Upon
// stopping, the player receives a score equal to the total value
// of all cards in the player's hand, or zero if the value exceeds 21.
// 
// Author:
//    Mike Gimelfarb (mgimelfarb@yahoo.ca)
//
////////////////////////////////////////////////////////////////////

domain blackjack {
        
    types {
		card : { @1, @2, @3, @4, @5, @6, @7, @8, @9, @10, @J, @Q, @K, @A };
    };
            
    pvariables {
		PROB(card) : { non-fluent, real, default = 0.07142857142 };

		total-value : { state-fluent, int, default = 0 };
		done : { state-fluent, bool, default = false };

		current-card : { interm-fluent, card };

		continue : { action-fluent, bool, default = true };
		ace-eleven : { action-fluent, bool, default = false };
    };
        
    cpfs {
		current-card = Discrete_{?c: card}(PROB(?c));

		total-value' = if (done | ~continue) then total-value else total-value + (switch(current-card){
			case @1: 1,
			case @2: 2,
			case @3: 3,
			case @4: 4,
			case @5: 5,
			case @6: 6,
			case @7: 7,
			case @8: 8,
			case @9: 9,
			case @10: 10,
			case @J: 11,
			case @Q: 12,
			case @K: 13,
			case @A: if(ace-eleven) then 11 else 1 
		});

		done' = done | ~continue;
    };

    termination {
		done | (total-value > 21);
    };
                
    reward = if(~done ^ done' ^ (total-value' <= 21)) then total-value' else 0;        
}
